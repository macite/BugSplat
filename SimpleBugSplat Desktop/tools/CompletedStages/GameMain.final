program GameMain;
uses SwinGame, sgTypes, SysUtils;

var score: Integer = 0;

procedure HandleSpriteEvent(bug: Sprite; event: SpriteEventKind);
begin
  if event = SpriteArrivedEvent then
  begin
    SpriteMoveTo(bug, RandomScreenPoint(), 1 + Rnd(10));
  end;

  if event = SpriteClickedEvent then
  begin
    SpriteStartAnimation(bug, 'splat');
    score := score + 1;
  end;

  if event = SpriteAnimationEndedEvent then
  begin
    SpriteSetPosition(bug, RandomScreenPoint());
    SpriteMoveTo(bug, RandomScreenPoint(), 3 + Rnd(10));
    SpriteStartAnimation(bug, 'fly');
  end;
end;

procedure Main();
var
  bug: Sprite;
begin
  OpenAudio();
  
  OpenGraphicsWindow('Bug Splat', 1024, 768);
  LoadDefaultColors();
  ShowSwinGameSplashScreen();

  LoadResourceBundle('BugSplat.txt');

  CallOnSpriteEvent(@HandleSpriteEvent);

  bug := CreateSprite('Buggy', 'BuggyAnimations');
  SpriteSetPosition(bug, RandomScreenPoint());
  SpriteMoveTo(bug, RandomScreenPoint(), 1 + Rnd(10));
  SpriteStartAnimation(bug, 'fly');

  repeat
    ProcessEvents();
    
    DrawBitmap('Background1', 0, 0);
    DrawBitmap('ScoreBackground', 25, 25);
    DrawText('Score ' + IntToStr(score), ColorBlack, 'ScoreFont', 45, 45);
    DrawAllSprites();
    RefreshScreen(60);

    UpdateAllSprites();
  until WindowCloseRequested();
    
  CloseAudio();
  ReleaseAllResources();
end;

begin
  Main();
end.
