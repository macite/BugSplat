program GameMain;
uses SwinGame, sgTypes, SysUtils;

var score: Integer = 0;
var level: Integer = 1;
var bugsRemaining: Integer = 1;

procedure ReviveBug(bug: Sprite);
begin
  SpriteSetPosition(bug, RandomScreenPoint());
  SpriteMoveTo(bug, RandomScreenPoint(), 1 + Rnd(10));
  SpriteStartAnimation(bug, 'fly');
end;

procedure HandleSpriteEvent(bug: Sprite; event: SpriteEventKind);
begin
  if event = SpriteArrivedEvent then
  begin
    SpriteMoveTo(bug, RandomScreenPoint(), 1 + Rnd(10));
  end;

  if (event = SpriteClickedEvent) and (SpriteAnimationName(bug) = 'fly') then
  begin
    SpriteStartAnimation(bug, 'splat');
    score := score + 1;
  end;

  if event = SpriteAnimationEndedEvent then
  begin
    bugsRemaining -= 1;

    if bugsRemaining = 0 then
    begin
      bug := CreateSprite('Buggy', 'BuggyAnimations');
      level +=  1;
      bugsRemaining := level;
      CallForAllSprites(@ReviveBug);
      ResetTimer('CountDownTimer');
    end;
  end;
end;

procedure Main();
var
  bug: Sprite;
begin
  OpenAudio();
  
  OpenGraphicsWindow('Bug Splat', 1024, 768);
  LoadDefaultColors();
  ShowSwinGameSplashScreen();

  LoadResourceBundle('BugSplat.txt');
  CreateTimer('CountDownTimer');

  CallOnSpriteEvent(@HandleSpriteEvent);

  bug := CreateSprite('Buggy', 'BuggyAnimations');
  SpriteSetPosition(bug, RandomScreenPoint());
  SpriteMoveTo(bug, RandomScreenPoint(), 1 + Rnd(10));
  SpriteStartAnimation(bug, 'fly');

  StartTimer('CountDownTimer');

  repeat
    ProcessEvents();
    
    DrawBitmap('Background1', 0, 0);

    DrawBitmap('ScoreBackground', 25, 25);
    DrawText('Score ' + IntToStr(score), ColorBlack, 'ScoreFont', 45, 45);

    DrawBitmap('ScoreBackground', 639, 25);
    DrawText('Level ' + IntToStr(level), ColorBlack, 'ScoreFont', 659, 45);

    DrawBitmap('ScoreBackground', 25, 678);
    DrawText('Time Remaining ' + IntToStr((10000 - TimerTicks('CountDownTimer')) div 1000), ColorBlack, 'ScoreFont', 45, 698);

    DrawAllSprites();

    if TimerTicks('CountDownTimer') > 10000 then
    begin
      StopTimer('CountDownTimer');
      ReleaseAllSprites();
    end;

    if TimerTicks('CountDownTimer') = 0 then
    begin
      DrawText('Game Over - space to restart', ColorWhite, 'MessageFont', 71, 302);
      DrawText('Game Over - space to restart', ColorBlack, 'MessageFont', 70, 300);

      if KeyTyped( vk_Space ) then
      begin
        bug := CreateSprite('Buggy', 'BuggyAnimations');
        ReviveBug(bug);
        level := 1;
        bugsRemaining := 1;
        score := 0;
        StartTimer('CountDownTimer');
      end;
    end;

    RefreshScreen(60);
    UpdateAllSprites();
  until WindowCloseRequested();
    
  CloseAudio();
  ReleaseAllResources();
end;

begin
  Main();
end.
