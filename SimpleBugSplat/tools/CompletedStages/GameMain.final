program GameMain;
uses SwinGame, sgTypes, SysUtils;

var score: Integer = 0;

procedure HandleSpriteEvent(bug: Sprite; event: String);
begin
  if event = 'Arrived' then
  begin
    SpriteMoveTo(bug, RandomScreenPoint(), 1 + Rnd(10));
  end;

  if event = 'Touched' then
  begin
    SpriteStartAnimation(bug, 'splat');
    score := score + 1;
  end;

  if event = 'AnimationEnded' then
  begin
    SpriteSetPosition(bug, RandomScreenPoint());
    SpriteMoveTo(bug, RandomScreenPoint(), 3 + Rnd(10));
    SpriteStartAnimation(bug, 'fly');
  end;
end;

procedure Main();
var
  bug: Sprite;
begin
  OpenAudio();
  
  OpenGraphicsWindow('Bug Splat', 1024, 768);
  LoadDefaultColors();
  ShowSwinGameSplashScreen();

  LoadResourceBundle('BugSplat.txt');

  bug := CreateSprite('Buggy', 'BuggyAnimations', @HandleSpriteEvent);
  SpriteSetPosition(bug, RandomScreenPoint());
  SpriteMoveTo(bug, RandomScreenPoint(), 1 + Rnd(10));
  SpriteStartAnimation(bug, 'fly');

  repeat
    ProcessEvents();
    
    DrawBitmap('Background1', 0, 0);
    DrawBitmap('ScoreBackground', 25, 25);
    DrawText('Score ' + IntToStr(score), ColorBlack, 'ScoreFont', 45, 45);
    DrawSprite(bug);
    RefreshScreen(60);

    UpdateSprite(bug);
  until WindowCloseRequested();
    
  CloseAudio();
  ReleaseAllResources();
end;

begin
  Main();
end.
